buildscript {
    ext {
        springBootVersion = '1.4.2.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'org.springframework.boot'
apply plugin: 'docker'

mainClassName = 'com.hstreb.productweb.Application'

bootRepackage {
    mainClass = mainClassName
}

group = 'com.hstreb'
version = "${currentVersion}-${currentBuild}"

compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-netflix:1.2.2.RELEASE'
        mavenBom "org.springframework.boot:spring-boot-starter-parent"
    }
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.cloud:spring-cloud-starter-eureka'
    compile 'org.springframework.cloud:spring-cloud-starter-feign'
    testCompile 'junit:junit:4.12'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2'
}

docker {
    maintainer = 'Humberto Streb ""hstreb@gmail.com""'
}

distDocker << {
    def imageTag = "${-> project.group}/${-> applicationName}:"
    def tag = project.version
    exec {
        commandLine "docker", "tag", imageTag + tag, imageTag + "latest"
    }
    setEnvironment 'EUREKA_PORT', '8761'
    setEnvironment 'EUREKA_URL', 'eureka'
}
